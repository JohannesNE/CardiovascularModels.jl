# Simple 3-compartment model

A simple model with a ventricle, an artery (aorta) and a vein, connected with some resistance between them.

```julia; results="hidden"
using DifferentialEquations, ModelingToolkit, CardiovascularModels, Plots

@variables t
D = Differential(t)

#left Ventricle
@named l_ventricle = Ventricle(Ees = 200e6,
    Vd = 0., V0 = 0., Î» = 33e3, P0 = 10.,
    R_out = 6e6)

@named card_driver = Driver()

# Aorta
@named aorta = Vessel(Ees = 200e6, V0 = 100e-6, R_out = 50e6)

eqs_driver = [
    l_ventricle.drv ~ card_driver.contraction
]

#@named vein = Const_Pressure(P = 1e3, R_out = 6e6, valve_out = true)
@named vein = Vessel(Ees = 5e6, V0 = 500e-6, R_out = 10e6, valve_out = true)



eqs_con = [
    connect(vein.out, l_ventricle.in),
    connect(l_ventricle.out, aorta.in),
    connect(aorta.out, vein.in)
]


eqs_comb = [eqs_driver; eqs_con]

# ODE parameters
volume_start = [l_ventricle.V => 1e-4, 
    aorta.V => 1.5e-4,
    vein.V => 2e-3]
time_span = (0.0, 10.0)

@named hemo_sys = ODESystem(eqs_comb)

@named connected = compose(hemo_sys, l_ventricle, aorta, vein, card_driver)

problem = ODEProblem(structural_simplify(connected), volume_start, time_span, [])

sol = solve(problem, Tsit5(), dtmax = 0.01, reltol = 1e-6)
```

```julia 
pascal2mmhg(t, pascal) = t, pascal * 0.00750062
m32ml(t, m3) = t, m3 * 1e6
```

## Volume plots
```julia
plot_v1 = plot(sol, vars=[(m32ml, 0, l_ventricle.V), (m32ml, 0, aorta.V)], tspan =  (1,6));
plot_v2 = plot(sol, vars=[(m32ml, 0, vein.V)], tspan =  (1,6));
plot(plot_v1, plot_v2, layout = (2,1))
```

## Pressure plots
```julia
plot_p1 = plot(sol, vars=[(pascal2mmhg, 0,l_ventricle.P), (pascal2mmhg, 0,aorta.P)], tspan =  (1,6));
plot_p2 = plot(sol, vars=[(pascal2mmhg, 0, vein.P)], tspan =  (1,6));
plot(plot_p1, plot_p2, layout = (2,1))
```